### LICENSE:
#
# Copyright (C) 2013 Ahmad Amarullah ( http://amarullz.com/ )
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#
###############################################################
#     _                _      _  ____               _      _  #
#    / \    _ __  ___ | |__  (_)|  _ \  _ __  ___  (_)  __| | #
#   / _ \  | '__|/ __|| '_ \ | || | | || '__|/ _ \ | | / _` | #
#  / ___ \ | |  | (__ | | | || || |_| || |  | (_) || || (_| | #
# /_/   \_\|_|   \___||_| |_||_||____/ |_|   \___/ |_| \__,_| #
#                                                             #
###############################################################


##
# Initializing Rom Information
#
ini_set("dp","3");
ini_set("rom_name",             "ArchiDroid");
ini_set("rom_version",          "2.1.5");
ini_set("rom_author",           "JustArchi");
ini_set("rom_device",           "Samsung Galaxy S3");
ini_set("rom_date",             "September XXth, 2013");

##
# Show Simple Splash
#
splash(
#  #-- Duration 2000ms / 2 seconds
    4000,
  
  #-- <AROMA Resource Dir>/sample.png
    "archidroidsplash"
);

#
# Set Small Font to Support all fonts
#
###fontresload( "0", "ttf/Roboto-Regular.ttf;ttf/DroidSansFallback.ttf;", "12" ); #-- Use sets of font (Font Family)
###fontresload( "1", "ttf/Roboto-Regular.ttf;ttf/DroidSansFallback.ttf;", "14" ); #-- Use sets of font (Font Family)
fontresload( "0", "ttf/Roboto-Regular.ttf", "12" );
fontresload( "1", "ttf/Roboto-Regular.ttf", "14" );

theme("touchwiz");




viewbox(
  #-- Title
    "Welcome!",
  
  #-- Text
    "Hello master, root@ArchiDroid here. Welcome to AROMA. You are about to install <b>"+
	  #-- Get Config Value
	  ini_get("rom_name")+
	"</b> for <b>"+ini_get("rom_device")+"</b>.\n\n"+
	
      "  Version:\t\t\t <b><#selectbg_g>"+ini_get("rom_version")+"</#></b>\n"+
      "  Released:\t\t <b><#selectbg_g>"+ini_get("rom_date")+"</#></b>\n\n\n"+

    "Press <b>Next</b> to continue the installation.",
  
  #-- Icon
    "@welcome"
);

textbox(
  #-- Title
    "Changelog",
  
  #-- Subtitle
    "",
  
  #-- Icon
    "@info",
  
  #-- Arg 4
    resread("_changelog.txt")
);

if
  confirm(
	#-- Title
	  "ArchiDroid Language",
	#-- Text
	  "Which language would you like to use?",
	#-- Icon (Optional)
	  "@confirm",
	#-- Yes Text
	  "English",
	#-- No Text
	  "Polski"
  )=="yes"
then
  loadlang("langs/en.lang");
else
  # Not yet!
  #loadlang("langs/pl.lang");
  loadlang("langs/en.lang");
endif;

agreebox(
  #-- Title
    "<~archidroid.tos>",
  
  #-- Subtitle / Description
    "",
  
  #-- Icon
    "@license",
  
  #-- Text Content ( Read from <AROMA Resource Dir>/read_me.txt )
    resread("read_me.txt"),
  
  #-- Checkbox Text
    "<~terms.check>",
  
  #-- Unchecked Alert Message
    "<~terms.confirm>"
);

agreebox(
  #-- Title
    "<~archidroid.tos>",
  
  #-- Subtitle / Description
    "",
  
  #-- Icon
    "@license",
  
  #-- Text Content ( Read from <AROMA Resource Dir>/read_me2.txt )
    resread("read_me2.txt"),
  
  #-- Checkbox Text
    "<~terms.check2>",
  
  #-- Unchecked Alert Message
    "<~terms.confirm2>"
);

pleasewait("Executing Shell...");
  
#-- Resource Exec
setvar(
  #-- Save Return Status
    "resexec_retstatus",
  
  #-- Run Shell
    resexec("exec/archidroid_detect.sh")
);

	if getvar("resexec_retstatus")=="0" || getvar("resexec_retstatus")=="1" then
	  if
		confirm(
		  #-- Title
			"ArchiDroid Detect",
		  #-- Text
			"<~archidroid.detect.fail>",
		  #-- Icon (Optional)
			"@info",
			#-- Yes Text
			  "<~archidroid.detect.fail.yes>",
			#-- No Text
			  "<~archidroid.detect.fail.no>"
		)=="yes"
	  then
		alert("ArchiDroid Detect","<~archidroid.detect.fail.alert.yes>","@info");
    writetmpfile(
      "archidroidwipe.prop",
      "wipe=yes\n"
    );
	  else
		alert("ArchiDroid Detect","<~archidroid.detect.fail.alert.no>","@alert");
		setvar("resexec_retstatus","2");
	  endif;
	endif;
	if getvar("resexec_retstatus")=="2" then
		if
		  confirm(
			#-- Title
			  "ArchiDroid Detect",
			#-- Text
			  "<~archidroid.detect.pass>",
			#-- Icon (Optional)
			  "@info",
			#-- Yes Text
			  "<~archidroid.detect.pass.yes>",
			#-- No Text
			  "<~archidroid.detect.pass.no>"
		  )=="yes"
		then
		  alert("ArchiDroid Detect","<~archidroid.detect.pass.alert.yes>","@info");
    writetmpfile(
      "archidroidwipe.prop",
      "wipe=no\n"
    );
		else
		  alert("ArchiDroid Detect","<~archidroid.detect.pass.alert.no>","@info");
    writetmpfile(
      "archidroidwipe.prop",
      "wipe=yes\n"
    );
		endif;
	endif;


form(
  "<~archidroid.preset>",
  "<~archidroid.preset.desc>",
  "icons/apps",
  "archidroidinstallmode.prop",
  
  "preset","<~archidroid.preset.group>","","group",
  "custom","<~archidroid.preset.custom>", "<~archidroid.preset.custom.desc>","select.selected",
  "barebones","<~archidroid.preset.barebones>","<~archidroid.preset.barebones.desc>","select",
  "archi","<~archidroid.preset.archi>","<~archidroid.preset.archi.desc>","select"
);

##################
### BARE BONES ###
##################

if file_getprop("/tmp/aroma-data/archidroidinstallmode.prop","preset") == "barebones" then
  writetmpfile(
    "archidroid.prop",

    "kernel=stock\n"+
    "dpi=320\n"+
    "launcher=stock\n"+
    "keyboard=stock\n"+
    "archidroidota=0\n"+
    "xposed=0\n"+
    "crossbreeder=0\n"+
    "updatemodem=0\n"+
    "updaterecovery=0\n"+
    "gapps=0\n"+
    "gquick=0\n"+
    "gcalendar=0\n"+
    "gchrome=0\n"+
    "gears=0\n"+
    "gkeep=0\n"+
    "gmail=0\n"+
    "gmaps=0\n"+
    "gvoice=0\n"+
    "ghangouts=0\n"+
    "gnow=0\n"+
    "gplus=0\n"+
    "titaniumbackup=0\n"+
    "zram=0\n"+
    "greenify=0\n"+
    "lmt=0\n"+
    "mxplayer=0\n"+
    "spiritfm=0\n"+
    "bbs=0\n"+
    "cooltool=0\n"+
    "rootbrowser=0\n"+
    "adobeflash=0\n"+
    "youtube=0\n"+
    "facebook=0\n"+
    "logcat=0\n"+
    "hdvoice=0\n"+
    "buildtweaks=0\n"+
    "openpdroid=0\n"+
    "nocell=0\n"
  );

  writetmpfile(
    "archidroidtemasek.prop",

    "preset=no\n"
  );

  writetmpfile(
    "archidroidboeffla.prop",

    "boeffla=no\n"
  );

  writetmpfile(
    "archidroidxposed.prop",

    "appsettings=0\n"+
    "xprivacy=0\n"
  );
endif;


#############
### ARCHI ###
#############

if file_getprop("/tmp/aroma-data/archidroidinstallmode.prop","preset") == "archi" then
  textbox(
    #-- Title
      "Preset",
    
    #-- Subtitle
      "Preset",
    
    #-- Icon
      "@welcome",
    
    #-- Arg 4
      resread("preset_archi.txt")
  );

  writetmpfile(
    "archidroid.prop",

    "kernel=temasek\n"+
    "dpi=241\n"+
    "launcher=nova\n"+
    "keyboard=both\n"+
    "archidroidota=0\n"+
    "xposed=1\n"+
    "crossbreeder=1\n"+
    "updatemodem=0\n"+
    "updaterecovery=0\n"+
    "gapps=1\n"+
    "gquick=0\n"+
    "gcalendar=0\n"+
    "gchrome=1\n"+
    "gears=1\n"+
    "gkeep=1\n"+
    "gmail=1\n"+
    "gmaps=1\n"+
    "gvoice=1\n"+
    "ghangouts=1\n"+
    "gnow=0\n"+
    "gplus=0\n"+
    "titaniumbackup=1\n"+
    "zram=0\n"+
    "greenify=1\n"+
    "lmt=1\n"+
    "mxplayer=1\n"+
    "spiritfm=1\n"+
    "bbs=1\n"+
    "cooltool=1\n"+
    "rootbrowser=1\n"+
    "adobeflash=1\n"+
    "youtube=1\n"+
    "facebook=1\n"+
    "logcat=1\n"+
    "hdvoice=1\n"+
    "buildtweaks=1\n"+
    "openpdroid=1\n"+
    "nocell=1\n"
  );

  writetmpfile(
    "archidroidtemasek.prop",

    "preset=ultra\n"
  );

  writetmpfile(
    "archidroidboeffla.prop",

    "boeffla=yesarchi\n"
  );

  writetmpfile(
    "archidroidxposed.prop",

    "appsettings=1\n"+
    "xprivacy=1\n"
  );
endif;

###############################
### CUSTOM TYPE INSTALATION ###
###############################

if file_getprop("/tmp/aroma-data/archidroidinstallmode.prop","preset") == "custom" then
  form(
    "<~archidroid.custom>",
    "<~archidroid.custom.desc>",
    "icons/apps",
    "archidroid.prop",
    
    "kernel","Kernel","","group",
    "temasek","Temasek Kernel [Boeffla/No ABB]","<~archidroid.kernel.temasek.desc>","select.selected",
    "yank","Yank555.lu Kernel","<~archidroid.kernel.yank.desc>","select",
    "googy","Googy-Max Kernel","<~archidroid.kernel.googy.desc>","select",
    "stock","Stock CM","<~archidroid.kernel.stock.desc>","select",

    "dpi","DPI","","group",
    "240","240 DPI","<~archidroid.dpi.240.desc>","select",
    "241","241 DPI","<~archidroid.dpi.241.desc>","select.selected",
    "280","280 DPI","<~archidroid.dpi.280.desc>","select",
    "320","320 DPI","<~archidroid.dpi.320.desc>","select",

    "launcher","Launcher","","group",
    "nova","Nova Launcher","Recommended for extra flexibility and performance","select.selected",
    "stock","AOSP Launcher","Coming from CM, also known as Trebuchet","select",

    "keyboard","Keyboard","","group",
    "touchpal","TouchPal Keyboard","Recommended as great and very flexible Swype/Curve keyboard","select.selected",
    "hackers","Hacker's Keyboard","Great alternative to TouchPal if you're not going to use swype/curve function","select",
    "both","TouchPal + Hacker's Keyboard","If you want to use both TouchPal and Hacker's (for example in terminal)","select",
    "stock","Stock CM","If you want to keep default CM keyboard","select",

    "other","Other","","group",
    "archidroidota","ArchiDroid OTA","Recommended by ArchiDroid. We'll install UpdateMe apk and enable OTA updates via build.prop","check.checked",
    "xposed","Xposed Framework","Allows using various of Xposed modules. Needs to be enabled manualy","check.checked",
    "crossbreeder","Crossbreeder","Recommended for extra performance and flexibility, not compatible with Seeder","check.checked",
    "updatemodem","Update Modem [XXEMG4]","If you want to have latest modem","check",
    "updaterecovery","Update Recovery [PhilZ]","If you want to have latest recovery","check",

    "googleapps","Google Apps","","group",
    "gapps","Google Apps","Core functions and apps from Google. DON'T DESELECT THIS","check.checked",
    "gquick","Google QuickSearchBox","May break Google Voice!","check",
    "gcalendar","Google Calendar [Inverted]","","check",
    "gchrome","Google Chrome [Inverted]","","check.checked",
    "gears","Google Ears [Inverted]","","check.checked",
    "gkeep","Google Keep [Inverted]","","check.checked",
    "gmail","Google Mail [Inverted]","","check.checked",
    "gmaps","Google Maps [Inverted]","","check.checked",
    "gvoice","Google Voice","","check.checked",
    "ghangouts","Google Hangouts [Inverted]","","check",
    "gnow","Google Now [Inverted]","","check",
    "gplus","Google Plus [Inverted]","","check",

    "archiapps","ArchiDroid Apps","","group",
    "titaniumbackup","Titanium Backup","Perfect solution for saving personal/app data between updates","check.checked",
    "zram","ZRAM Status","Useful if you want to monitor your zram usage (only available in custom kernels)","check",
    "greenify","Greenify","Useful if you want to hibernate any app, which saves your battery","check.checked",
    "lmt","LMT","Useful tool for executing a command binded to specific gesture on the screen, also known as [pie] in Android 4.2","check.checked",
    "mxplayer","MXPlayer","Probably best video player for Android","check.checked",
    "spiritfm","Spirit FM","Free radio working with CM","check.checked",
    "bbs","BetterBatteryStats","Gives you power to view more specific battery stats - especially wakelocks","check.checked",
    "cooltool","CoolTool","Shows some useful statistics on the top bar","check.checked",
    "rootbrowser","Root Browser","Very cool browser extended by optional root access","check.checked",
    "adobeflash","Adobe Flash Player [Inverted]","Adds flash content support. Does not work with Google Chrome","check.checked",
    "youtube","YouTube [Inverted]","Inverted version of YouTube app","check.checked",
    "facebook","Facebook [Inverted]","Inverted version of Facebook and Messenger","check.checked",

    "architweaks","ArchiDroid Tweaks","","group",
    "logcat","Logcat Bootanimation","Changes default bootaimation to logcat output - powered by logcat lite","check.checked",
    "hdvoice","HD Voice","Enables HD Voice if available","check.checked",
    "buildtweaks","Add build.prop Tweaks","Deeper sleep and disable debug to improve performance","check.checked",
    "openpdroid","OpenPDroid [EXPERIMENTAL]","Apply OpenPDroid patch after install if available","check",
    "nocell","Remove Cell Broadcasts","In general considered as bloatware. Uncheck if you want to keep this","check.checked"
  );

  textbox(
   "DEBUG",
   "Form generated prop file",
   "@update",
   readtmpfile("archidroid.prop")
  );

  ### ArchiDroid Extra ###
  if file_getprop("/tmp/aroma-data/archidroid.prop","kernel") == "temasek" then
    form(
      "ArchiDroid Extra Customization",
      "Please modify settings to your liking",
      "icons/apps",
      "archidroidtemasek.prop",

      "preset","Temasek Kernel Preset","","group",
      "balanced","ArchiDroid Balanced Config","Based on Optimized zzmoove, zram, boeffla sound and more","select",
      "powersave","ArchiDroid PowerSave Config","Highly powersave config based on Yank's Battery zzmoove with lower hotplugging","select.selected",
      "ultra","ArchiDroid ULTRA PowerSave Config","ULTRA powersave config based on Archi's Ultra Battery zzmoove with LCDFreq","select",
      "no","No Config","Not recommended because you'll miss most of the great features. Choose only if you're going to create your own STweaks config","select"
    );
  endif;

  if file_getprop("/tmp/aroma-data/archidroid.prop","kernel") == "temasek" || file_getprop("archidroid.prop","kernel") == "yank" then
    form(
      "ArchiDroid Extra Customization",
      "Please modify settings to your liking",
      "icons/apps",
      "archidroidboeffla.prop",

      "boeffla","Boeffla Sound Control","","group",
      "yesarchi","Yes with Archi's Audiophile","Highly recommended. It enhances audio quality by installing Boeffla SC and tweaking according to Archi's taste","select.selected",
      "yes","Yes","If you want to create your own Boeffla SC preset later","select",
      "no","No","If you don't want to install Boeffla SC","select"
    );
  endif;

  if file_getprop("/tmp/aroma-data/archidroid.prop","xposed") == "1" then
    form(
      "ArchiDroid Extra Customization",
      "Please modify settings to your liking",
      "icons/apps",
      "archidroidxposed.prop",

      "modules","Xposed Modules","","group",
      "appsettings","App Settings","Allows changing app settings. Extremely useful tool","check.checked",
      "xprivacy","XPrivacy","Perfect module which prevents leaking of private data, restricts access and more","check.checked"
    );
  endif;
endif;




ini_set("text_next", "Flash");

viewbox(
    "Ready to install",
    "You are now ready to begin installation.\n\n"+
    "Press <b>Flash</b> to start.\n\n"+
    "If you want to review or change any of your installation settings, press <b>Back</b>.",
    "@alert"
);

ini_set("text_next", "Finish");

  install(
	#-- Title
      "Installing",
	
	#-- Installation Process message
	  "<b>"+ini_get("rom_name")+"</b> is being installed.\n\n"+
      "Please wait while installation wizard installs <b>"+ini_get("rom_name")+
	  "</b>. WARNING! In case of freeze please hold on power button to restart installation!",
	
	#-- Installation Icon
      "@install",
	
	#-- Installation Finish Message
      "The installation wizard has successfully installed <b>"+ini_get("rom_name")+
	  "</b>. Press <b>Finish</b> to continue."
);

checkviewbox(
  #-- Title
    "Installation Completed",
  
  #-- Text
    "<#selectbg_g><b>Congratulation...</b></#>\n\n"+
    "<b>"+ini_get("rom_name")+"</b> has been flashed onto your device.\n\n"+
    "Installer Status: "+getvar("retstatus")+"\n\n",
	
  #-- Icon
    "@exit",

  #-- Checkbox Text
    "Reboot Automatically",

  #-- Initial Checkbox value ( 0=unchecked, 1=checked ) -  (Optional, default:0)
    "1",

  #-- Save checked value in variable "reboot_it" (Optional)
    "reboot_it"
);


###
#
# Check if reboot checkbox was checked
if getvar("reboot_it")=="1" then

	# reboot("onfinish");   - Reboot if anything finished
	# reboot("now");        - Reboot Directly
	# reboot("disable");    - If you set reboot("onfinish") before, use this command to revert it.

	reboot("onfinish");

endif;
